<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Polynomial.NormalForm.Definition</title><link rel="stylesheet" href="Agda.css"></head><body><pre><a id="1" class="Symbol">{-#</a> <a id="5" class="Keyword">OPTIONS</a> <a id="13" class="Option">--without-K</a> <a id="25" class="Option">--safe</a> <a id="32" class="Symbol">#-}</a>

<a id="37" class="Comment">-- Definitions for the types of a polynomial stored in sparse horner</a>
<a id="106" class="Comment">-- normal form.</a>
<a id="122" class="Comment">--</a>
<a id="125" class="Comment">-- These definitions ensure that the polynomial is actually in fully</a>
<a id="194" class="Comment">-- canonical form, with no trailing zeroes, etc.</a>

<a id="244" class="Keyword">open</a> <a id="249" class="Keyword">import</a> <a id="256" href="Polynomial.Parameters.html" class="Module">Polynomial.Parameters</a>

<a id="279" class="Keyword">module</a> <a id="286" href="Polynomial.NormalForm.Definition.html" class="Module">Polynomial.NormalForm.Definition</a>
  <a id="321" class="Symbol">{</a><a id="322" href="Polynomial.NormalForm.Definition.html#322" class="Bound">a</a><a id="323" class="Symbol">}</a>
  <a id="327" class="Symbol">(</a><a id="328" href="Polynomial.NormalForm.Definition.html#328" class="Bound">coeffs</a> <a id="335" class="Symbol">:</a> <a id="337" href="Polynomial.Parameters.html#562" class="Record">RawCoeff</a> <a id="346" href="Polynomial.NormalForm.Definition.html#322" class="Bound">a</a><a id="347" class="Symbol">)</a>
  <a id="351" class="Keyword">where</a>

<a id="358" class="Keyword">open</a> <a id="363" class="Keyword">import</a> <a id="370" href="Polynomial.NormalForm.InjectionIndex.html" class="Module">Polynomial.NormalForm.InjectionIndex</a>

<a id="408" class="Keyword">open</a> <a id="413" class="Keyword">import</a> <a id="420" href="Relation.Nullary.html" class="Module">Relation.Nullary</a> <a id="437" class="Keyword">using</a> <a id="443" class="Symbol">(</a><a id="444" href="Relation.Nullary.html#500" class="Function Operator">¬_</a><a id="446" class="Symbol">)</a>
<a id="448" class="Keyword">open</a> <a id="453" class="Keyword">import</a> <a id="460" href="Level.html" class="Module">Level</a>            <a id="477" class="Keyword">using</a> <a id="483" class="Symbol">(</a><a id="484" href="Agda.Primitive.html#657" class="Primitive Operator">_⊔_</a><a id="487" class="Symbol">;</a> <a id="489" href="Level.html#394" class="Record">Lift</a><a id="493" class="Symbol">)</a>
<a id="495" class="Keyword">open</a> <a id="500" class="Keyword">import</a> <a id="507" href="Data.Empty.html" class="Module">Data.Empty</a>       <a id="524" class="Keyword">using</a> <a id="530" class="Symbol">(</a><a id="531" href="Data.Empty.html#279" class="Datatype">⊥</a><a id="532" class="Symbol">)</a>
<a id="534" class="Keyword">open</a> <a id="539" class="Keyword">import</a> <a id="546" href="Data.Unit.html" class="Module">Data.Unit</a>        <a id="563" class="Keyword">using</a> <a id="569" class="Symbol">(</a><a id="570" href="Agda.Builtin.Unit.html#69" class="Record">⊤</a><a id="571" class="Symbol">)</a>
<a id="573" class="Keyword">open</a> <a id="578" class="Keyword">import</a> <a id="585" href="Data.Nat.html" class="Module">Data.Nat</a>         <a id="602" class="Keyword">using</a> <a id="608" class="Symbol">(</a><a id="609" href="Agda.Builtin.Nat.html#97" class="Datatype">ℕ</a><a id="610" class="Symbol">;</a> <a id="612" href="Agda.Builtin.Nat.html#128" class="InductiveConstructor">suc</a><a id="615" class="Symbol">;</a> <a id="617" href="Agda.Builtin.Nat.html#115" class="InductiveConstructor">zero</a><a id="621" class="Symbol">)</a>
<a id="623" class="Keyword">open</a> <a id="628" class="Keyword">import</a> <a id="635" href="Function.html" class="Module">Function</a>         <a id="652" class="Keyword">using</a> <a id="658" class="Symbol">(</a><a id="659" href="Function.html#805" class="Function Operator">_∘_</a><a id="662" class="Symbol">)</a>
<a id="664" class="Keyword">open</a> <a id="669" class="Keyword">import</a> <a id="676" href="Data.Maybe.html" class="Module">Data.Maybe</a>       <a id="693" class="Keyword">using</a> <a id="699" class="Symbol">(</a><a id="700" href="Data.Maybe.Base.html#710" class="Datatype">Maybe</a><a id="705" class="Symbol">;</a> <a id="707" href="Data.Maybe.Base.html#748" class="InductiveConstructor">just</a><a id="711" class="Symbol">;</a> <a id="713" href="Data.Maybe.Base.html#778" class="InductiveConstructor">nothing</a><a id="720" class="Symbol">)</a>
<a id="722" class="Keyword">open</a> <a id="727" class="Keyword">import</a> <a id="734" href="Data.Bool.html" class="Module">Data.Bool</a>        <a id="751" class="Keyword">using</a> <a id="757" class="Symbol">(</a><a id="758" href="Data.Bool.Base.html#795" class="Function">T</a><a id="759" class="Symbol">)</a>
<a id="761" class="Keyword">open</a> <a id="766" class="Keyword">import</a> <a id="773" href="Data.Product.html" class="Module">Data.Product</a>     <a id="790" class="Keyword">using</a> <a id="796" class="Symbol">(</a><a id="797" href="Data.Product.html#1389" class="Function Operator">_×_</a><a id="800" class="Symbol">;</a> <a id="802" href="Agda.Builtin.Sigma.html#139" class="InductiveConstructor Operator">_,_</a><a id="805" class="Symbol">)</a>
<a id="807" class="Keyword">open</a> <a id="812" class="Keyword">import</a> <a id="819" href="Data.List.Kleene.html" class="Module">Data.List.Kleene</a> <a id="836" class="Keyword">public</a>

<a id="844" class="Keyword">infixl</a> <a id="851" class="Number">6</a> <a id="853" href="Polynomial.NormalForm.Definition.html#927" class="InductiveConstructor Operator">_Δ_</a>
<a id="857" class="Keyword">record</a> <a id="PowInd"></a><a id="864" href="Polynomial.NormalForm.Definition.html#864" class="Record">PowInd</a> <a id="871" class="Symbol">{</a><a id="872" href="Polynomial.NormalForm.Definition.html#872" class="Bound">c</a><a id="873" class="Symbol">}</a> <a id="875" class="Symbol">(</a><a id="876" href="Polynomial.NormalForm.Definition.html#876" class="Bound">C</a> <a id="878" class="Symbol">:</a> <a id="880" class="PrimitiveType">Set</a> <a id="884" href="Polynomial.NormalForm.Definition.html#872" class="Bound">c</a><a id="885" class="Symbol">)</a> <a id="887" class="Symbol">:</a> <a id="889" class="PrimitiveType">Set</a> <a id="893" href="Polynomial.NormalForm.Definition.html#872" class="Bound">c</a> <a id="895" class="Keyword">where</a>
  <a id="903" class="Keyword">inductive</a>
  <a id="915" class="Keyword">constructor</a> <a id="PowInd._Δ_"></a><a id="927" href="Polynomial.NormalForm.Definition.html#927" class="InductiveConstructor Operator">_Δ_</a>
  <a id="933" class="Keyword">field</a>
    <a id="PowInd.coeff"></a><a id="943" href="Polynomial.NormalForm.Definition.html#943" class="Field">coeff</a> <a id="949" class="Symbol">:</a> <a id="951" href="Polynomial.NormalForm.Definition.html#876" class="Bound">C</a>
    <a id="PowInd.pow"></a><a id="957" href="Polynomial.NormalForm.Definition.html#957" class="Field">pow</a>   <a id="963" class="Symbol">:</a> <a id="965" href="Agda.Builtin.Nat.html#97" class="Datatype">ℕ</a>
<a id="967" class="Keyword">open</a> <a id="972" href="Polynomial.NormalForm.Definition.html#864" class="Module">PowInd</a> <a id="979" class="Keyword">public</a>

<a id="987" class="Keyword">open</a> <a id="992" href="Polynomial.Parameters.html#562" class="Module">RawCoeff</a> <a id="1001" href="Polynomial.NormalForm.Definition.html#328" class="Bound">coeffs</a>

<a id="1009" class="Keyword">mutual</a>
  <a id="1018" class="Comment">-- A Polynomial is indexed by the number of variables it contains.</a>
  <a id="1087" class="Keyword">infixl</a> <a id="1094" class="Number">6</a> <a id="1096" href="Polynomial.NormalForm.Definition.html#1166" class="InductiveConstructor Operator">_Π_</a>
  <a id="1102" class="Keyword">record</a> <a id="Poly"></a><a id="1109" href="Polynomial.NormalForm.Definition.html#1109" class="Record">Poly</a> <a id="1114" class="Symbol">(</a><a id="1115" href="Polynomial.NormalForm.Definition.html#1115" class="Bound">n</a> <a id="1117" class="Symbol">:</a> <a id="1119" href="Agda.Builtin.Nat.html#97" class="Datatype">ℕ</a><a id="1120" class="Symbol">)</a> <a id="1122" class="Symbol">:</a> <a id="1124" class="PrimitiveType">Set</a> <a id="1128" href="Polynomial.NormalForm.Definition.html#322" class="Bound">a</a> <a id="1130" class="Keyword">where</a>
    <a id="1140" class="Keyword">inductive</a>
    <a id="1154" class="Keyword">constructor</a> <a id="Poly._Π_"></a><a id="1166" href="Polynomial.NormalForm.Definition.html#1166" class="InductiveConstructor Operator">_Π_</a>
    <a id="1174" class="Keyword">field</a>
      <a id="1186" class="Symbol">{</a><a id="Poly.i"></a><a id="1187" href="Polynomial.NormalForm.Definition.html#1187" class="Field">i</a><a id="1188" class="Symbol">}</a>  <a id="1191" class="Symbol">:</a> <a id="1193" href="Agda.Builtin.Nat.html#97" class="Datatype">ℕ</a>
      <a id="Poly.flat"></a><a id="1201" href="Polynomial.NormalForm.Definition.html#1201" class="Field">flat</a> <a id="1206" class="Symbol">:</a> <a id="1208" href="Polynomial.NormalForm.Definition.html#1247" class="Datatype">FlatPoly</a> <a id="1217" href="Polynomial.NormalForm.Definition.html#1187" class="Field">i</a>
      <a id="Poly.i≤n"></a><a id="1225" href="Polynomial.NormalForm.Definition.html#1225" class="Field">i≤n</a>  <a id="1230" class="Symbol">:</a> <a id="1232" href="Polynomial.NormalForm.Definition.html#1187" class="Field">i</a> <a id="1234" href="Data.Nat.Base.html#2376" class="Datatype Operator">≤′</a> <a id="1237" href="Polynomial.NormalForm.Definition.html#1115" class="Bound">n</a>

  <a id="1242" class="Keyword">data</a> <a id="FlatPoly"></a><a id="1247" href="Polynomial.NormalForm.Definition.html#1247" class="Datatype">FlatPoly</a> <a id="1256" class="Symbol">:</a> <a id="1258" href="Agda.Builtin.Nat.html#97" class="Datatype">ℕ</a> <a id="1260" class="Symbol">→</a> <a id="1262" class="PrimitiveType">Set</a> <a id="1266" href="Polynomial.NormalForm.Definition.html#322" class="Bound">a</a> <a id="1268" class="Keyword">where</a>
    <a id="FlatPoly.Κ"></a><a id="1278" href="Polynomial.NormalForm.Definition.html#1278" class="InductiveConstructor">Κ</a> <a id="1280" class="Symbol">:</a> <a id="1282" href="Algebra.html#12255" class="Function">Carrier</a> <a id="1290" class="Symbol">→</a> <a id="1292" href="Polynomial.NormalForm.Definition.html#1247" class="Datatype">FlatPoly</a> <a id="1301" href="Agda.Builtin.Nat.html#115" class="InductiveConstructor">zero</a>
    <a id="FlatPoly.Σ"></a><a id="1310" href="Polynomial.NormalForm.Definition.html#1310" class="InductiveConstructor">Σ</a> <a id="1312" class="Symbol">:</a> <a id="1314" class="Symbol">∀</a> <a id="1316" class="Symbol">{</a><a id="1317" href="Polynomial.NormalForm.Definition.html#1317" class="Bound">n</a><a id="1318" class="Symbol">}</a> <a id="1320" class="Symbol">→</a> <a id="1322" class="Symbol">(</a><a id="1323" href="Polynomial.NormalForm.Definition.html#1323" class="Bound">xs</a> <a id="1326" class="Symbol">:</a> <a id="1328" href="Polynomial.NormalForm.Definition.html#1809" class="Function">Coeff</a> <a id="1334" href="Polynomial.NormalForm.Definition.html#1317" class="Bound">n</a> <a id="1336" href="Data.List.Kleene.html#233" class="Record Operator">⁺</a><a id="1337" class="Symbol">)</a> <a id="1339" class="Symbol">→</a> <a id="1341" class="Symbol">.{</a><a id="1343" href="Polynomial.NormalForm.Definition.html#1343" class="Bound">xn</a> <a id="1346" class="Symbol">:</a> <a id="1348" href="Polynomial.NormalForm.Definition.html#2529" class="Function">Norm</a> <a id="1353" href="Polynomial.NormalForm.Definition.html#1323" class="Bound">xs</a><a id="1355" class="Symbol">}</a> <a id="1357" class="Symbol">→</a> <a id="1359" href="Polynomial.NormalForm.Definition.html#1247" class="Datatype">FlatPoly</a> <a id="1368" class="Symbol">(</a><a id="1369" href="Agda.Builtin.Nat.html#128" class="InductiveConstructor">suc</a> <a id="1373" href="Polynomial.NormalForm.Definition.html#1317" class="Bound">n</a><a id="1374" class="Symbol">)</a>

  <a id="1379" class="Comment">-- A list of coefficients, paired with the exponent *gap* from the</a>
  <a id="1448" class="Comment">-- preceding coefficient. In other words, to represent the</a>
  <a id="1509" class="Comment">-- polynomial:</a>
  <a id="1526" class="Comment">--</a>
  <a id="1531" class="Comment">--   3 + 2x² + 4x⁵ + 2x⁷</a>
  <a id="1558" class="Comment">--</a>
  <a id="1563" class="Comment">-- We write:</a>
  <a id="1578" class="Comment">--</a>
  <a id="1583" class="Comment">--   [(3,0),(2,1),(4,2),(2,1)]</a>
  <a id="1616" class="Comment">--</a>
  <a id="1621" class="Comment">-- Which can be thought of as a representation of the expression:</a>
  <a id="1689" class="Comment">--</a>
  <a id="1694" class="Comment">--   x⁰ * (3 + x * x¹ * (2 + x * x² * (4 + x * x¹ * (2 + x * 0))))</a>
  <a id="1763" class="Comment">--</a>
  <a id="1768" class="Comment">-- This is sparse Horner normal form.</a>

  <a id="Coeff"></a><a id="1809" href="Polynomial.NormalForm.Definition.html#1809" class="Function">Coeff</a> <a id="1815" class="Symbol">:</a> <a id="1817" href="Agda.Builtin.Nat.html#97" class="Datatype">ℕ</a> <a id="1819" class="Symbol">→</a> <a id="1821" class="PrimitiveType">Set</a> <a id="1825" href="Polynomial.NormalForm.Definition.html#322" class="Bound">a</a>
  <a id="1829" href="Polynomial.NormalForm.Definition.html#1809" class="Function">Coeff</a> <a id="1835" href="Polynomial.NormalForm.Definition.html#1835" class="Bound">n</a> <a id="1837" class="Symbol">=</a> <a id="1839" href="Polynomial.NormalForm.Definition.html#864" class="Record">PowInd</a> <a id="1846" class="Symbol">(</a><a id="1847" href="Polynomial.NormalForm.Definition.html#2022" class="Record">NonZero</a> <a id="1855" href="Polynomial.NormalForm.Definition.html#1835" class="Bound">n</a><a id="1856" class="Symbol">)</a>

  <a id="1861" class="Comment">-- We disallow zeroes in the coefficient list. This condition alone</a>
  <a id="1931" class="Comment">-- is enough to ensure a unique representation for any polynomial.</a>
  <a id="2000" class="Keyword">infixl</a> <a id="2007" class="Number">6</a> <a id="2009" href="Polynomial.NormalForm.Definition.html#2082" class="InductiveConstructor Operator">_≠0</a>
  <a id="2015" class="Keyword">record</a> <a id="NonZero"></a><a id="2022" href="Polynomial.NormalForm.Definition.html#2022" class="Record">NonZero</a> <a id="2030" class="Symbol">(</a><a id="2031" href="Polynomial.NormalForm.Definition.html#2031" class="Bound">i</a> <a id="2033" class="Symbol">:</a> <a id="2035" href="Agda.Builtin.Nat.html#97" class="Datatype">ℕ</a><a id="2036" class="Symbol">)</a> <a id="2038" class="Symbol">:</a> <a id="2040" class="PrimitiveType">Set</a> <a id="2044" href="Polynomial.NormalForm.Definition.html#322" class="Bound">a</a> <a id="2046" class="Keyword">where</a>
    <a id="2056" class="Keyword">inductive</a>
    <a id="2070" class="Keyword">constructor</a> <a id="NonZero._≠0"></a><a id="2082" href="Polynomial.NormalForm.Definition.html#2082" class="InductiveConstructor Operator">_≠0</a>
    <a id="2090" class="Keyword">field</a>
      <a id="NonZero.poly"></a><a id="2102" href="Polynomial.NormalForm.Definition.html#2102" class="Field">poly</a> <a id="2107" class="Symbol">:</a> <a id="2109" href="Polynomial.NormalForm.Definition.html#1109" class="Record">Poly</a> <a id="2114" href="Polynomial.NormalForm.Definition.html#2031" class="Bound">i</a>
      <a id="2122" class="Symbol">.{</a><a id="NonZero.poly≠0"></a><a id="2124" href="Polynomial.NormalForm.Definition.html#2124" class="Field">poly≠0</a><a id="2130" class="Symbol">}</a> <a id="2132" class="Symbol">:</a> <a id="2134" href="Relation.Nullary.html#500" class="Function Operator">¬</a> <a id="2136" href="Polynomial.NormalForm.Definition.html#2271" class="Function">Zero</a> <a id="2141" href="Polynomial.NormalForm.Definition.html#2102" class="Field">poly</a>

  <a id="2149" class="Comment">-- This predicate is used (in its negation) to ensure that no</a>
  <a id="2213" class="Comment">-- coefficient is zero, preventing any trailing zeroes.</a>
  <a id="Zero"></a><a id="2271" href="Polynomial.NormalForm.Definition.html#2271" class="Function">Zero</a> <a id="2276" class="Symbol">:</a> <a id="2278" class="Symbol">∀</a> <a id="2280" class="Symbol">{</a><a id="2281" href="Polynomial.NormalForm.Definition.html#2281" class="Bound">n</a><a id="2282" class="Symbol">}</a> <a id="2284" class="Symbol">→</a> <a id="2286" href="Polynomial.NormalForm.Definition.html#1109" class="Record">Poly</a> <a id="2291" href="Polynomial.NormalForm.Definition.html#2281" class="Bound">n</a> <a id="2293" class="Symbol">→</a> <a id="2295" class="PrimitiveType">Set</a>
  <a id="2301" href="Polynomial.NormalForm.Definition.html#2271" class="Function">Zero</a> <a id="2306" class="Symbol">(</a><a id="2307" href="Polynomial.NormalForm.Definition.html#1278" class="InductiveConstructor">Κ</a> <a id="2309" href="Polynomial.NormalForm.Definition.html#2309" class="Bound">x</a> <a id="2311" href="Polynomial.NormalForm.Definition.html#1166" class="InductiveConstructor Operator">Π</a> <a id="2313" class="Symbol">_)</a> <a id="2316" class="Symbol">=</a> <a id="2318" href="Data.Bool.Base.html#795" class="Function">T</a> <a id="2320" class="Symbol">(</a><a id="2321" href="Polynomial.Parameters.html#629" class="Field">Zero-C</a> <a id="2328" href="Polynomial.NormalForm.Definition.html#2309" class="Bound">x</a><a id="2329" class="Symbol">)</a>
  <a id="2333" href="Polynomial.NormalForm.Definition.html#2271" class="Function">Zero</a> <a id="2338" class="Symbol">(</a><a id="2339" href="Polynomial.NormalForm.Definition.html#1310" class="InductiveConstructor">Σ</a> <a id="2341" class="Symbol">_</a> <a id="2343" href="Polynomial.NormalForm.Definition.html#1166" class="InductiveConstructor Operator">Π</a> <a id="2345" class="Symbol">_)</a> <a id="2348" class="Symbol">=</a> <a id="2350" href="Data.Empty.html#279" class="Datatype">⊥</a>

  <a id="2355" class="Comment">-- This predicate is used to ensure that all polynomials are in</a>
  <a id="2421" class="Comment">-- normal form: if a particular level is constant, than it can</a>
  <a id="2486" class="Comment">-- be collapsed into the level below it.</a>
  <a id="Norm"></a><a id="2529" href="Polynomial.NormalForm.Definition.html#2529" class="Function">Norm</a> <a id="2534" class="Symbol">:</a> <a id="2536" class="Symbol">∀</a> <a id="2538" class="Symbol">{</a><a id="2539" href="Polynomial.NormalForm.Definition.html#2539" class="Bound">i</a><a id="2540" class="Symbol">}</a> <a id="2542" class="Symbol">→</a> <a id="2544" href="Polynomial.NormalForm.Definition.html#1809" class="Function">Coeff</a> <a id="2550" href="Polynomial.NormalForm.Definition.html#2539" class="Bound">i</a> <a id="2552" href="Data.List.Kleene.html#233" class="Record Operator">⁺</a> <a id="2554" class="Symbol">→</a> <a id="2556" class="PrimitiveType">Set</a>
  <a id="2562" href="Polynomial.NormalForm.Definition.html#2529" class="Function">Norm</a> <a id="2567" class="Symbol">(_</a> <a id="2570" href="Polynomial.NormalForm.Definition.html#927" class="InductiveConstructor Operator">Δ</a> <a id="2572" href="Agda.Builtin.Nat.html#115" class="InductiveConstructor">zero</a>  <a id="2578" href="Data.List.Kleene.html#296" class="InductiveConstructor Operator">&amp;</a> <a id="2580" href="Data.List.Kleene.html#178" class="InductiveConstructor">[]</a><a id="2582" class="Symbol">)</a>    <a id="2587" class="Symbol">=</a> <a id="2589" href="Data.Empty.html#279" class="Datatype">⊥</a>
  <a id="2593" href="Polynomial.NormalForm.Definition.html#2529" class="Function">Norm</a> <a id="2598" class="Symbol">(_</a> <a id="2601" href="Polynomial.NormalForm.Definition.html#927" class="InductiveConstructor Operator">Δ</a> <a id="2603" href="Agda.Builtin.Nat.html#115" class="InductiveConstructor">zero</a>  <a id="2609" href="Data.List.Kleene.html#296" class="InductiveConstructor Operator">&amp;</a> <a id="2611" href="Data.List.Kleene.html#192" class="InductiveConstructor Operator">[</a> <a id="2613" class="Symbol">_</a> <a id="2615" href="Data.List.Kleene.html#192" class="InductiveConstructor Operator">]</a><a id="2616" class="Symbol">)</a> <a id="2618" class="Symbol">=</a> <a id="2620" href="Agda.Builtin.Unit.html#69" class="Record">⊤</a>
  <a id="2624" href="Polynomial.NormalForm.Definition.html#2529" class="Function">Norm</a> <a id="2629" class="Symbol">(_</a> <a id="2632" href="Polynomial.NormalForm.Definition.html#927" class="InductiveConstructor Operator">Δ</a> <a id="2634" href="Agda.Builtin.Nat.html#128" class="InductiveConstructor">suc</a> <a id="2638" class="Symbol">_</a> <a id="2640" href="Data.List.Kleene.html#296" class="InductiveConstructor Operator">&amp;</a> <a id="2642" class="Symbol">_)</a>     <a id="2649" class="Symbol">=</a> <a id="2651" href="Agda.Builtin.Unit.html#69" class="Record">⊤</a>
<a id="2653" class="Keyword">open</a> <a id="2658" href="Polynomial.NormalForm.Definition.html#2022" class="Module">NonZero</a> <a id="2666" class="Keyword">public</a>
<a id="2673" class="Keyword">open</a> <a id="2678" href="Polynomial.NormalForm.Definition.html#1109" class="Module">Poly</a> <a id="2683" class="Keyword">public</a>
</pre></body></html>