<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Polynomial.NormalForm.Semantics</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Symbol">{-#</a> <a id="5" class="Keyword">OPTIONS</a> <a id="13" class="Pragma">--without-K</a> <a id="25" class="Pragma">--safe</a> <a id="32" class="Symbol">#-}</a>

<a id="37" class="Keyword">open</a> <a id="42" class="Keyword">import</a> <a id="49" href="Polynomial.Parameters.html" class="Module">Polynomial.Parameters</a>

<a id="72" class="Comment">-- &quot;Evaluating&quot; a polynomial, using Horner&#39;s method.</a>

<a id="126" class="Keyword">module</a> <a id="133" href="Polynomial.NormalForm.Semantics.html" class="Module">Polynomial.NormalForm.Semantics</a>
  <a id="167" class="Symbol">{</a><a id="168" href="Polynomial.NormalForm.Semantics.html#168" class="Bound">r₁</a> <a id="171" href="Polynomial.NormalForm.Semantics.html#171" class="Bound">r₂</a> <a id="174" href="Polynomial.NormalForm.Semantics.html#174" class="Bound">r₃</a><a id="176" class="Symbol">}</a>
  <a id="180" class="Symbol">(</a><a id="181" href="Polynomial.NormalForm.Semantics.html#181" class="Bound">homo</a> <a id="186" class="Symbol">:</a> <a id="188" href="Polynomial.Parameters.html#795" class="Record">Homomorphism</a> <a id="201" href="Polynomial.NormalForm.Semantics.html#168" class="Bound">r₁</a> <a id="204" href="Polynomial.NormalForm.Semantics.html#171" class="Bound">r₂</a> <a id="207" href="Polynomial.NormalForm.Semantics.html#174" class="Bound">r₃</a><a id="209" class="Symbol">)</a>
  <a id="213" class="Keyword">where</a>

<a id="220" class="Keyword">open</a> <a id="225" class="Keyword">import</a> <a id="232" href="Data.Nat.html" class="Module">Data.Nat</a>     <a id="245" class="Keyword">using</a> <a id="251" class="Symbol">(</a><a id="252" href="Agda.Builtin.Nat.html#165" class="Datatype">ℕ</a><a id="253" class="Symbol">;</a> <a id="255" href="Agda.Builtin.Nat.html#196" class="InductiveConstructor">suc</a><a id="258" class="Symbol">;</a> <a id="260" href="Agda.Builtin.Nat.html#183" class="InductiveConstructor">zero</a><a id="264" class="Symbol">)</a>
<a id="266" class="Keyword">open</a> <a id="271" class="Keyword">import</a> <a id="278" href="Data.Vec.html" class="Module">Data.Vec</a>     <a id="291" class="Keyword">using</a> <a id="297" class="Symbol">(</a><a id="298" href="Data.Vec.html#887" class="Datatype">Vec</a><a id="301" class="Symbol">;</a> <a id="303" href="Data.Vec.html#923" class="InductiveConstructor">[]</a><a id="305" class="Symbol">;</a> <a id="307" href="Data.Vec.html#942" class="InductiveConstructor Operator">_∷_</a><a id="310" class="Symbol">)</a>
<a id="312" class="Keyword">open</a> <a id="317" class="Keyword">import</a> <a id="324" href="Data.List.html" class="Module">Data.List</a>    <a id="337" class="Keyword">using</a> <a id="343" class="Symbol">(</a><a id="344" href="Data.List.Base.html#8781" class="InductiveConstructor">[]</a><a id="346" class="Symbol">;</a> <a id="348" href="Agda.Builtin.List.html#173" class="InductiveConstructor Operator">_∷_</a><a id="351" class="Symbol">)</a>
<a id="353" class="Keyword">open</a> <a id="358" class="Keyword">import</a> <a id="365" href="Data.Product.html" class="Module">Data.Product</a> <a id="378" class="Keyword">using</a> <a id="384" class="Symbol">(</a><a id="385" href="Agda.Builtin.Sigma.html#209" class="InductiveConstructor Operator">_,_</a><a id="388" class="Symbol">;</a> <a id="390" href="Data.Product.html#1162" class="Function Operator">_×_</a><a id="393" class="Symbol">)</a>

<a id="396" class="Keyword">open</a> <a id="401" class="Keyword">import</a> <a id="408" href="Polynomial.NormalForm.InjectionIndex.html" class="Module">Polynomial.NormalForm.InjectionIndex</a>
<a id="445" class="Keyword">open</a> <a id="450" href="Polynomial.Parameters.html#795" class="Module">Homomorphism</a> <a id="463" href="Polynomial.NormalForm.Semantics.html#181" class="Bound">homo</a>
<a id="468" class="Keyword">open</a> <a id="473" class="Keyword">import</a> <a id="480" href="Polynomial.NormalForm.Definition.html" class="Module">Polynomial.NormalForm.Definition</a> <a id="513" href="Polynomial.Parameters.html#862" class="Field">coeffs</a>

<a id="521" class="Keyword">open</a> <a id="526" class="Keyword">import</a> <a id="533" href="Polynomial.Exponentiation.html" class="Module">Polynomial.Exponentiation</a> <a id="559" href="Algebra.Solver.Ring.AlmostCommutativeRing.html#2040" class="Function">rawRing</a>

<a id="drop"></a><a id="568" href="Polynomial.NormalForm.Semantics.html#568" class="Function">drop</a> <a id="573" class="Symbol">:</a> <a id="575" class="Symbol">∀</a> <a id="577" class="Symbol">{</a><a id="578" href="Polynomial.NormalForm.Semantics.html#578" class="Bound">i</a> <a id="580" href="Polynomial.NormalForm.Semantics.html#580" class="Bound">n</a><a id="581" class="Symbol">}</a> <a id="583" class="Symbol">→</a> <a id="585" href="Polynomial.NormalForm.Semantics.html#578" class="Bound">i</a> <a id="587" href="Data.Nat.Base.html#2460" class="Datatype Operator">≤′</a> <a id="590" href="Polynomial.NormalForm.Semantics.html#580" class="Bound">n</a> <a id="592" class="Symbol">→</a> <a id="594" href="Data.Vec.html#887" class="Datatype">Vec</a> <a id="598" href="Algebra.Solver.Ring.AlmostCommutativeRing.html#1263" class="Function">Carrier</a> <a id="606" href="Polynomial.NormalForm.Semantics.html#580" class="Bound">n</a> <a id="608" class="Symbol">→</a> <a id="610" href="Data.Vec.html#887" class="Datatype">Vec</a> <a id="614" href="Algebra.Solver.Ring.AlmostCommutativeRing.html#1263" class="Function">Carrier</a> <a id="622" href="Polynomial.NormalForm.Semantics.html#578" class="Bound">i</a>
<a id="624" href="Polynomial.NormalForm.Semantics.html#568" class="Function">drop</a> <a id="629" href="Data.Nat.Base.html#2491" class="InductiveConstructor">≤′-refl</a> <a id="637" href="Polynomial.NormalForm.Semantics.html#637" class="Bound">Ρ</a> <a id="639" class="Symbol">=</a> <a id="641" href="Polynomial.NormalForm.Semantics.html#637" class="Bound">Ρ</a>
<a id="643" href="Polynomial.NormalForm.Semantics.html#568" class="Function">drop</a> <a id="648" class="Symbol">(</a><a id="649" href="Data.Nat.Base.html#2534" class="InductiveConstructor">≤′-step</a> <a id="657" href="Polynomial.NormalForm.Semantics.html#657" class="Bound">si≤n</a><a id="661" class="Symbol">)</a> <a id="663" class="Symbol">(_</a> <a id="666" href="Data.Vec.html#942" class="InductiveConstructor Operator">∷</a> <a id="668" href="Polynomial.NormalForm.Semantics.html#668" class="Bound">Ρ</a><a id="669" class="Symbol">)</a> <a id="671" class="Symbol">=</a> <a id="673" href="Polynomial.NormalForm.Semantics.html#568" class="Function">drop</a> <a id="678" href="Polynomial.NormalForm.Semantics.html#657" class="Bound">si≤n</a> <a id="683" href="Polynomial.NormalForm.Semantics.html#668" class="Bound">Ρ</a>

<a id="vec-uncons"></a><a id="686" href="Polynomial.NormalForm.Semantics.html#686" class="Function">vec-uncons</a> <a id="697" class="Symbol">:</a> <a id="699" class="Symbol">∀</a> <a id="701" class="Symbol">{</a><a id="702" href="Polynomial.NormalForm.Semantics.html#702" class="Bound">n</a><a id="703" class="Symbol">}</a> <a id="705" class="Symbol">→</a> <a id="707" href="Data.Vec.html#887" class="Datatype">Vec</a> <a id="711" href="Algebra.Solver.Ring.AlmostCommutativeRing.html#1263" class="Function">Carrier</a> <a id="719" class="Symbol">(</a><a id="720" href="Agda.Builtin.Nat.html#196" class="InductiveConstructor">suc</a> <a id="724" href="Polynomial.NormalForm.Semantics.html#702" class="Bound">n</a><a id="725" class="Symbol">)</a> <a id="727" class="Symbol">→</a> <a id="729" href="Algebra.Solver.Ring.AlmostCommutativeRing.html#1263" class="Function">Carrier</a> <a id="737" href="Data.Product.html#1162" class="Function Operator">×</a> <a id="739" href="Data.Vec.html#887" class="Datatype">Vec</a> <a id="743" href="Algebra.Solver.Ring.AlmostCommutativeRing.html#1263" class="Function">Carrier</a> <a id="751" href="Polynomial.NormalForm.Semantics.html#702" class="Bound">n</a>
<a id="753" href="Polynomial.NormalForm.Semantics.html#686" class="Function">vec-uncons</a> <a id="764" class="Symbol">(</a><a id="765" href="Polynomial.NormalForm.Semantics.html#765" class="Bound">x</a> <a id="767" href="Data.Vec.html#942" class="InductiveConstructor Operator">∷</a> <a id="769" href="Polynomial.NormalForm.Semantics.html#769" class="Bound">xs</a><a id="771" class="Symbol">)</a> <a id="773" class="Symbol">=</a> <a id="775" href="Polynomial.NormalForm.Semantics.html#765" class="Bound">x</a> <a id="777" href="Agda.Builtin.Sigma.html#209" class="InductiveConstructor Operator">,</a> <a id="779" href="Polynomial.NormalForm.Semantics.html#769" class="Bound">xs</a>
<a id="782" class="Symbol">{-#</a> <a id="786" class="Keyword">INLINE</a> <a id="793" href="Polynomial.NormalForm.Semantics.html#686" class="Function">vec-uncons</a> <a id="804" class="Symbol">#-}</a>

<a id="drop-1"></a><a id="809" href="Polynomial.NormalForm.Semantics.html#809" class="Function">drop-1</a> <a id="816" class="Symbol">:</a> <a id="818" class="Symbol">∀</a> <a id="820" class="Symbol">{</a><a id="821" href="Polynomial.NormalForm.Semantics.html#821" class="Bound">i</a> <a id="823" href="Polynomial.NormalForm.Semantics.html#823" class="Bound">n</a><a id="824" class="Symbol">}</a> <a id="826" class="Symbol">→</a> <a id="828" href="Agda.Builtin.Nat.html#196" class="InductiveConstructor">suc</a> <a id="832" href="Polynomial.NormalForm.Semantics.html#821" class="Bound">i</a> <a id="834" href="Data.Nat.Base.html#2460" class="Datatype Operator">≤′</a> <a id="837" href="Polynomial.NormalForm.Semantics.html#823" class="Bound">n</a> <a id="839" class="Symbol">→</a> <a id="841" href="Data.Vec.html#887" class="Datatype">Vec</a> <a id="845" href="Algebra.Solver.Ring.AlmostCommutativeRing.html#1263" class="Function">Carrier</a> <a id="853" href="Polynomial.NormalForm.Semantics.html#823" class="Bound">n</a> <a id="855" class="Symbol">→</a> <a id="857" href="Algebra.Solver.Ring.AlmostCommutativeRing.html#1263" class="Function">Carrier</a> <a id="865" href="Data.Product.html#1162" class="Function Operator">×</a> <a id="867" href="Data.Vec.html#887" class="Datatype">Vec</a> <a id="871" href="Algebra.Solver.Ring.AlmostCommutativeRing.html#1263" class="Function">Carrier</a> <a id="879" href="Polynomial.NormalForm.Semantics.html#821" class="Bound">i</a>
<a id="881" href="Polynomial.NormalForm.Semantics.html#809" class="Function">drop-1</a> <a id="888" href="Polynomial.NormalForm.Semantics.html#888" class="Bound">si≤n</a> <a id="893" href="Polynomial.NormalForm.Semantics.html#893" class="Bound">xs</a> <a id="896" class="Symbol">=</a> <a id="898" href="Polynomial.NormalForm.Semantics.html#686" class="Function">vec-uncons</a> <a id="909" class="Symbol">(</a><a id="910" href="Polynomial.NormalForm.Semantics.html#568" class="Function">drop</a> <a id="915" href="Polynomial.NormalForm.Semantics.html#888" class="Bound">si≤n</a> <a id="920" href="Polynomial.NormalForm.Semantics.html#893" class="Bound">xs</a><a id="922" class="Symbol">)</a>
<a id="924" class="Symbol">{-#</a> <a id="928" class="Keyword">INLINE</a> <a id="935" href="Polynomial.NormalForm.Semantics.html#809" class="Function">drop-1</a> <a id="942" class="Symbol">#-}</a>

<a id="_*⟨_⟩^_"></a><a id="947" href="Polynomial.NormalForm.Semantics.html#947" class="Function Operator">_*⟨_⟩^_</a> <a id="955" class="Symbol">:</a> <a id="957" href="Algebra.Solver.Ring.AlmostCommutativeRing.html#1263" class="Function">Carrier</a> <a id="965" class="Symbol">→</a> <a id="967" href="Algebra.Solver.Ring.AlmostCommutativeRing.html#1263" class="Function">Carrier</a> <a id="975" class="Symbol">→</a> <a id="977" href="Agda.Builtin.Nat.html#165" class="Datatype">ℕ</a> <a id="979" class="Symbol">→</a> <a id="981" href="Algebra.Solver.Ring.AlmostCommutativeRing.html#1263" class="Function">Carrier</a>
<a id="989" href="Polynomial.NormalForm.Semantics.html#989" class="Bound">x</a> <a id="991" href="Polynomial.NormalForm.Semantics.html#947" class="Function Operator">*⟨</a> <a id="994" href="Polynomial.NormalForm.Semantics.html#994" class="Bound">ρ</a> <a id="996" href="Polynomial.NormalForm.Semantics.html#947" class="Function Operator">⟩^</a> <a id="999" href="Agda.Builtin.Nat.html#183" class="InductiveConstructor">zero</a> <a id="1004" class="Symbol">=</a> <a id="1006" href="Polynomial.NormalForm.Semantics.html#989" class="Bound">x</a>
<a id="1008" href="Polynomial.NormalForm.Semantics.html#1008" class="Bound">x</a> <a id="1010" href="Polynomial.NormalForm.Semantics.html#947" class="Function Operator">*⟨</a> <a id="1013" href="Polynomial.NormalForm.Semantics.html#1013" class="Bound">ρ</a> <a id="1015" href="Polynomial.NormalForm.Semantics.html#947" class="Function Operator">⟩^</a> <a id="1018" href="Agda.Builtin.Nat.html#196" class="InductiveConstructor">suc</a> <a id="1022" href="Polynomial.NormalForm.Semantics.html#1022" class="Bound">i</a> <a id="1024" class="Symbol">=</a> <a id="1026" href="Polynomial.NormalForm.Semantics.html#1013" class="Bound">ρ</a> <a id="1028" href="Polynomial.Exponentiation.html#898" class="Function Operator">^</a> <a id="1030" href="Polynomial.NormalForm.Semantics.html#1022" class="Bound">i</a> <a id="1032" href="Polynomial.Exponentiation.html#898" class="Function Operator">+1</a> <a id="1035" href="Algebra.Solver.Ring.AlmostCommutativeRing.html#1385" class="Function Operator">*</a> <a id="1037" href="Polynomial.NormalForm.Semantics.html#1008" class="Bound">x</a>
<a id="1039" class="Symbol">{-#</a> <a id="1043" class="Keyword">INLINE</a> <a id="1050" href="Polynomial.NormalForm.Semantics.html#947" class="Function Operator">_*⟨_⟩^_</a> <a id="1058" class="Symbol">#-}</a>

<a id="1063" class="Comment">--------------------------------------------------------------------------------</a>
<a id="1144" class="Comment">-- Evaluation</a>
<a id="1158" class="Comment">--------------------------------------------------------------------------------</a>
<a id="1239" class="Comment">-- Why do we have three functions here? Why are they so weird looking?</a>
<a id="1310" class="Comment">--</a>
<a id="1313" class="Comment">-- These three functions are the main bottleneck for all of the proofs: as such,</a>
<a id="1394" class="Comment">-- slight changes can dramatically affect the length of proof code.</a>
<a id="1462" class="Keyword">mutual</a>
  <a id="_⟦∷⟧_"></a><a id="1471" href="Polynomial.NormalForm.Semantics.html#1471" class="Function Operator">_⟦∷⟧_</a> <a id="1477" class="Symbol">:</a> <a id="1479" class="Symbol">∀</a> <a id="1481" class="Symbol">{</a><a id="1482" href="Polynomial.NormalForm.Semantics.html#1482" class="Bound">n</a><a id="1483" class="Symbol">}</a> <a id="1485" class="Symbol">→</a> <a id="1487" href="Polynomial.NormalForm.Definition.html#958" class="Record">Poly</a> <a id="1492" href="Polynomial.NormalForm.Semantics.html#1482" class="Bound">n</a> <a id="1494" href="Data.Product.html#1162" class="Function Operator">×</a> <a id="1496" href="Polynomial.NormalForm.Definition.html#1658" class="Function">Coeff</a> <a id="1502" href="Polynomial.NormalForm.Semantics.html#1482" class="Bound">n</a> <a id="1504" href="Data.List.Kleene.html#177" class="Datatype Operator">⋆</a> <a id="1506" class="Symbol">→</a> <a id="1508" href="Algebra.Solver.Ring.AlmostCommutativeRing.html#1263" class="Function">Carrier</a> <a id="1516" href="Data.Product.html#1162" class="Function Operator">×</a> <a id="1518" href="Data.Vec.html#887" class="Datatype">Vec</a> <a id="1522" href="Algebra.Solver.Ring.AlmostCommutativeRing.html#1263" class="Function">Carrier</a> <a id="1530" href="Polynomial.NormalForm.Semantics.html#1482" class="Bound">n</a> <a id="1532" class="Symbol">→</a> <a id="1534" href="Algebra.Solver.Ring.AlmostCommutativeRing.html#1263" class="Function">Carrier</a>
  <a id="1544" class="Symbol">(</a><a id="1545" href="Polynomial.NormalForm.Semantics.html#1545" class="Bound">x</a> <a id="1547" href="Agda.Builtin.Sigma.html#209" class="InductiveConstructor Operator">,</a> <a id="1549" href="Data.List.Kleene.html#214" class="InductiveConstructor">[]</a><a id="1551" class="Symbol">)</a> <a id="1553" href="Polynomial.NormalForm.Semantics.html#1471" class="Function Operator">⟦∷⟧</a> <a id="1557" class="Symbol">(</a><a id="1558" href="Polynomial.NormalForm.Semantics.html#1558" class="Bound">ρ</a> <a id="1560" href="Agda.Builtin.Sigma.html#209" class="InductiveConstructor Operator">,</a> <a id="1562" href="Polynomial.NormalForm.Semantics.html#1562" class="Bound">ρs</a><a id="1564" class="Symbol">)</a> <a id="1566" class="Symbol">=</a> <a id="1568" href="Polynomial.NormalForm.Semantics.html#1774" class="Function Operator">⟦</a> <a id="1570" href="Polynomial.NormalForm.Semantics.html#1545" class="Bound">x</a> <a id="1572" href="Polynomial.NormalForm.Semantics.html#1774" class="Function Operator">⟧</a> <a id="1574" href="Polynomial.NormalForm.Semantics.html#1562" class="Bound">ρs</a>
  <a id="1579" class="Symbol">(</a><a id="1580" href="Polynomial.NormalForm.Semantics.html#1580" class="Bound">x</a> <a id="1582" href="Agda.Builtin.Sigma.html#209" class="InductiveConstructor Operator">,</a> <a id="1584" href="Data.List.Kleene.html#228" class="InductiveConstructor Operator">[</a> <a id="1586" href="Polynomial.NormalForm.Semantics.html#1586" class="Bound">xs</a> <a id="1589" href="Data.List.Kleene.html#228" class="InductiveConstructor Operator">]</a><a id="1590" class="Symbol">)</a> <a id="1592" href="Polynomial.NormalForm.Semantics.html#1471" class="Function Operator">⟦∷⟧</a> <a id="1596" class="Symbol">(</a><a id="1597" href="Polynomial.NormalForm.Semantics.html#1597" class="Bound">ρ</a> <a id="1599" href="Agda.Builtin.Sigma.html#209" class="InductiveConstructor Operator">,</a> <a id="1601" href="Polynomial.NormalForm.Semantics.html#1601" class="Bound">ρs</a><a id="1603" class="Symbol">)</a> <a id="1605" class="Symbol">=</a> <a id="1607" href="Polynomial.NormalForm.Semantics.html#1597" class="Bound">ρ</a> <a id="1609" href="Algebra.Solver.Ring.AlmostCommutativeRing.html#1385" class="Function Operator">*</a> <a id="1611" href="Polynomial.NormalForm.Semantics.html#1642" class="Function Operator">Σ⟦</a> <a id="1614" href="Polynomial.NormalForm.Semantics.html#1586" class="Bound">xs</a> <a id="1617" href="Polynomial.NormalForm.Semantics.html#1642" class="Function Operator">⟧</a> <a id="1619" class="Symbol">(</a><a id="1620" href="Polynomial.NormalForm.Semantics.html#1597" class="Bound">ρ</a> <a id="1622" href="Agda.Builtin.Sigma.html#209" class="InductiveConstructor Operator">,</a> <a id="1624" href="Polynomial.NormalForm.Semantics.html#1601" class="Bound">ρs</a><a id="1626" class="Symbol">)</a> <a id="1628" href="Algebra.Solver.Ring.AlmostCommutativeRing.html#1343" class="Function Operator">+</a> <a id="1630" href="Polynomial.NormalForm.Semantics.html#1774" class="Function Operator">⟦</a> <a id="1632" href="Polynomial.NormalForm.Semantics.html#1580" class="Bound">x</a> <a id="1634" href="Polynomial.NormalForm.Semantics.html#1774" class="Function Operator">⟧</a> <a id="1636" href="Polynomial.NormalForm.Semantics.html#1601" class="Bound">ρs</a>

  <a id="Σ⟦_⟧"></a><a id="1642" href="Polynomial.NormalForm.Semantics.html#1642" class="Function Operator">Σ⟦_⟧</a> <a id="1647" class="Symbol">:</a> <a id="1649" class="Symbol">∀</a> <a id="1651" class="Symbol">{</a><a id="1652" href="Polynomial.NormalForm.Semantics.html#1652" class="Bound">n</a><a id="1653" class="Symbol">}</a> <a id="1655" class="Symbol">→</a> <a id="1657" href="Polynomial.NormalForm.Definition.html#1658" class="Function">Coeff</a> <a id="1663" href="Polynomial.NormalForm.Semantics.html#1652" class="Bound">n</a> <a id="1665" href="Data.List.Kleene.html#269" class="Record Operator">⁺</a> <a id="1667" class="Symbol">→</a> <a id="1669" class="Symbol">(</a><a id="1670" href="Algebra.Solver.Ring.AlmostCommutativeRing.html#1263" class="Function">Carrier</a> <a id="1678" href="Data.Product.html#1162" class="Function Operator">×</a> <a id="1680" href="Data.Vec.html#887" class="Datatype">Vec</a> <a id="1684" href="Algebra.Solver.Ring.AlmostCommutativeRing.html#1263" class="Function">Carrier</a> <a id="1692" href="Polynomial.NormalForm.Semantics.html#1652" class="Bound">n</a><a id="1693" class="Symbol">)</a> <a id="1695" class="Symbol">→</a> <a id="1697" href="Algebra.Solver.Ring.AlmostCommutativeRing.html#1263" class="Function">Carrier</a>
  <a id="1707" href="Polynomial.NormalForm.Semantics.html#1642" class="Function Operator">Σ⟦</a> <a id="1710" href="Polynomial.NormalForm.Semantics.html#1710" class="Bound">x</a> <a id="1712" href="Polynomial.NormalForm.Definition.html#1931" class="InductiveConstructor Operator">≠0</a> <a id="1715" href="Polynomial.NormalForm.Definition.html#776" class="InductiveConstructor Operator">Δ</a> <a id="1717" href="Polynomial.NormalForm.Semantics.html#1717" class="Bound">i</a> <a id="1719" href="Data.List.Kleene.html#332" class="InductiveConstructor Operator">&amp;</a> <a id="1721" href="Polynomial.NormalForm.Semantics.html#1721" class="Bound">xs</a> <a id="1724" href="Polynomial.NormalForm.Semantics.html#1642" class="Function Operator">⟧</a> <a id="1726" class="Symbol">(</a><a id="1727" href="Polynomial.NormalForm.Semantics.html#1727" class="Bound">ρ</a> <a id="1729" href="Agda.Builtin.Sigma.html#209" class="InductiveConstructor Operator">,</a> <a id="1731" href="Polynomial.NormalForm.Semantics.html#1731" class="Bound">ρs</a><a id="1733" class="Symbol">)</a> <a id="1735" class="Symbol">=</a> <a id="1737" class="Symbol">((</a><a id="1739" href="Polynomial.NormalForm.Semantics.html#1710" class="Bound">x</a> <a id="1741" href="Agda.Builtin.Sigma.html#209" class="InductiveConstructor Operator">,</a> <a id="1743" href="Polynomial.NormalForm.Semantics.html#1721" class="Bound">xs</a><a id="1745" class="Symbol">)</a> <a id="1747" href="Polynomial.NormalForm.Semantics.html#1471" class="Function Operator">⟦∷⟧</a> <a id="1751" class="Symbol">(</a><a id="1752" href="Polynomial.NormalForm.Semantics.html#1727" class="Bound">ρ</a> <a id="1754" href="Agda.Builtin.Sigma.html#209" class="InductiveConstructor Operator">,</a> <a id="1756" href="Polynomial.NormalForm.Semantics.html#1731" class="Bound">ρs</a><a id="1758" class="Symbol">))</a> <a id="1761" href="Polynomial.NormalForm.Semantics.html#947" class="Function Operator">*⟨</a> <a id="1764" href="Polynomial.NormalForm.Semantics.html#1727" class="Bound">ρ</a> <a id="1766" href="Polynomial.NormalForm.Semantics.html#947" class="Function Operator">⟩^</a> <a id="1769" href="Polynomial.NormalForm.Semantics.html#1717" class="Bound">i</a>

  <a id="⟦_⟧"></a><a id="1774" href="Polynomial.NormalForm.Semantics.html#1774" class="Function Operator">⟦_⟧</a> <a id="1778" class="Symbol">:</a> <a id="1780" class="Symbol">∀</a> <a id="1782" class="Symbol">{</a><a id="1783" href="Polynomial.NormalForm.Semantics.html#1783" class="Bound">n</a><a id="1784" class="Symbol">}</a> <a id="1786" class="Symbol">→</a> <a id="1788" href="Polynomial.NormalForm.Definition.html#958" class="Record">Poly</a> <a id="1793" href="Polynomial.NormalForm.Semantics.html#1783" class="Bound">n</a> <a id="1795" class="Symbol">→</a> <a id="1797" href="Data.Vec.html#887" class="Datatype">Vec</a> <a id="1801" href="Algebra.Solver.Ring.AlmostCommutativeRing.html#1263" class="Function">Carrier</a> <a id="1809" href="Polynomial.NormalForm.Semantics.html#1783" class="Bound">n</a> <a id="1811" class="Symbol">→</a> <a id="1813" href="Algebra.Solver.Ring.AlmostCommutativeRing.html#1263" class="Function">Carrier</a>
  <a id="1823" href="Polynomial.NormalForm.Semantics.html#1774" class="Function Operator">⟦</a> <a id="1825" href="Polynomial.NormalForm.Definition.html#1127" class="InductiveConstructor">Κ</a> <a id="1827" href="Polynomial.NormalForm.Semantics.html#1827" class="Bound">x</a>  <a id="1830" href="Polynomial.NormalForm.Definition.html#1015" class="InductiveConstructor Operator">Π</a> <a id="1832" href="Polynomial.NormalForm.Semantics.html#1832" class="Bound">i≤n</a> <a id="1836" href="Polynomial.NormalForm.Semantics.html#1774" class="Function Operator">⟧</a> <a id="1838" class="Symbol">_</a> <a id="1840" class="Symbol">=</a> <a id="1842" href="Algebra.Solver.Ring.AlmostCommutativeRing.html#2551" class="Function Operator">⟦</a> <a id="1844" href="Polynomial.NormalForm.Semantics.html#1827" class="Bound">x</a> <a id="1846" href="Algebra.Solver.Ring.AlmostCommutativeRing.html#2551" class="Function Operator">⟧ᵣ</a>
  <a id="1851" href="Polynomial.NormalForm.Semantics.html#1774" class="Function Operator">⟦</a> <a id="1853" href="Polynomial.NormalForm.Definition.html#1159" class="InductiveConstructor">Σ</a> <a id="1855" href="Polynomial.NormalForm.Semantics.html#1855" class="Bound">xs</a> <a id="1858" href="Polynomial.NormalForm.Definition.html#1015" class="InductiveConstructor Operator">Π</a> <a id="1860" href="Polynomial.NormalForm.Semantics.html#1860" class="Bound">i≤n</a> <a id="1864" href="Polynomial.NormalForm.Semantics.html#1774" class="Function Operator">⟧</a> <a id="1866" href="Polynomial.NormalForm.Semantics.html#1866" class="Bound">Ρ</a> <a id="1868" class="Symbol">=</a> <a id="1870" href="Polynomial.NormalForm.Semantics.html#1642" class="Function Operator">Σ⟦</a> <a id="1873" href="Polynomial.NormalForm.Semantics.html#1855" class="Bound">xs</a> <a id="1876" href="Polynomial.NormalForm.Semantics.html#1642" class="Function Operator">⟧</a> <a id="1878" class="Symbol">(</a><a id="1879" href="Polynomial.NormalForm.Semantics.html#809" class="Function">drop-1</a> <a id="1886" href="Polynomial.NormalForm.Semantics.html#1860" class="Bound">i≤n</a> <a id="1890" href="Polynomial.NormalForm.Semantics.html#1866" class="Bound">Ρ</a><a id="1891" class="Symbol">)</a>
<a id="1893" class="Symbol">{-#</a> <a id="1897" class="Keyword">INLINE</a> <a id="1904" href="Polynomial.NormalForm.Semantics.html#1774" class="Function Operator">⟦_⟧</a> <a id="1908" class="Symbol">#-}</a>
<a id="1912" class="Symbol">{-#</a> <a id="1916" class="Keyword">INLINE</a> <a id="1923" href="Polynomial.NormalForm.Semantics.html#1642" class="Function Operator">Σ⟦_⟧</a> <a id="1928" class="Symbol">#-}</a>
<a id="1932" class="Comment">--------------------------------------------------------------------------------</a>
<a id="2013" class="Comment">-- Performance</a>
<a id="2028" class="Comment">--------------------------------------------------------------------------------</a>
<a id="2109" class="Comment">-- As you might imagine, the implementation of the functions above seriously</a>
<a id="2186" class="Comment">-- affect performance. What you might not realise, though, is that the most</a>
<a id="2262" class="Comment">-- important component is the *order of the arguments*. For instance, if</a>
<a id="2335" class="Comment">-- we change:</a>
<a id="2349" class="Comment">--</a>
<a id="2352" class="Comment">--   (x , xs) ⟦∷⟧ (ρ , ρs) = ρ * Σ⟦ xs ⟧ (ρ , ρs) + ⟦ x ⟧ ρs</a>
<a id="2413" class="Comment">--</a>
<a id="2416" class="Comment">-- To:</a>
<a id="2423" class="Comment">--</a>
<a id="2426" class="Comment">--   (x , xs) ⟦∷⟧ (ρ , ρs) = ⟦ x ⟧ ρs +  Σ⟦ xs ⟧ (ρ , ρs) * ρ</a>
<a id="2488" class="Comment">--</a>
<a id="2491" class="Comment">-- We get a function that&#39;s several orders of magnitude slower!</a>
</pre></body></html>